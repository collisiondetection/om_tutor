<?xml version="1.0"?>
<!--
  Licensed to the Apache Software Foundation (ASF) under one
  or more contributor license agreements.  See the NOTICE file
  distributed with this work for additional information
  regarding copyright ownership.  The ASF licenses this file
  to you under the Apache License, Version 2.0 (the
  "License"); you may not use this file except in compliance
  with the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing,
  software distributed under the License is distributed on an
  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
  KIND, either express or implied.  See the License for the
  specific language governing permissions and limitations
  under the License.
-->
<nettest:NetworkTest
		xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:nettest="org.apache.openmeetings.nettest.*">
	<fx:Script><![CDATA[
		import org.apache.openmeetings.net.RestCall;
		import org.apache.openmeetings.net.UploadPostHelper;

		private var hostUrl:String;
		private var request:URLRequest;
		protected var loader:RestCall;
		private var loaderInited:Boolean = false;
		protected var startTime:int;
		private var vars:ByteArray;

		override protected function init():void {
			super.init();
			title = lbl('upl');
			resultText = true;
		}

		private function initLoader():void {
			hostUrl = getBaseUrl() + "services/networktest";
			loader = new RestCall({
				url: hostUrl
				, method: URLRequestMethod.POST
				, dataFormat: URLLoaderDataFormat.BINARY
				, contentType: "multipart/form-data; charset=utf-8; boundary=" + UploadPostHelper.getBoundary()
				, errorCallback: onError
				, successCallback: onComplete
			});
			vars = UploadPostHelper.getPostData('aaa', getData());
			debug("initLoader:: ");
			loaderInited = true;
		}

		override protected function start():void {
			debug("start:: " + request);
			if (!loaderInited) {
				initLoader();
			}

			debug("going to start:: " + loader);
			log(lbl('report.start') + " " + hostUrl);
			startTime = getTimer();
			request = loader.load("nettest", vars);
			debug("load:: " + request);
		}

		override protected function forcedStop():void {
			loader.close();
			log("stopped");
		}

		private function onComplete(e:Event):void {
			debug("onComplete:: " + e);
			if (running) {
				var endTime:Number = getTimer();
				var loadTime:Number = endTime - startTime;
				var loadTimeSec:Number = loadTime / 1000;
				var mBytes:Number = request.data.length / (1024 * 1024);
				var speed:Number = mBytes / loadTimeSec;

				var report:String = "report:\n";
				report += lbl('upl.bytes') + ": " + mBytes + " " + lbl('mb') + "\n";
				report += lbl('upl.time') + ": " + loadTimeSec + " " + lbl('sec') + "\n";
				report += lbl('upl.speed') + ": " + speed + " " + lbl('mb') + "/" + lbl('sec') + "\n";

				result.text = formatNumber(speed) + "\n" +  lbl('mb') + "/" + lbl('sec');
				log(report);
				setPassed();
			}
		}

		private function onError(e:ErrorEvent):void {
			debug("onError:: " + e);
			if (running) {
				log(lbl('report.error') + " - " + e);
				setFailed();
			}
		}

		private function getData():ByteArray {
			var result:ByteArray = new ByteArray();
			for (var i:int = 0; i < 64 * 16384; ++i) {
				result.writeByte(i % 256);
			}
			result.position = 0;
			return result;
		}
	]]></fx:Script>
</nettest:NetworkTest>
